/*
filename    assassin
password    mfxmas
changedby   Ratlin
EmailAdd
request     compile
version     14
END HEADER*/


#include <macros.h>

#define _GUILD_ASSASSIN
#define ASSASSIN_ONGOING "Assassin Quest Ongoing"
#define ASSASSIN_COMPLETE "Assassin Quest Complete"

#define QUEST_ASSASSIN_ENTRY "Assassin Guild Entry"

#define ROOM_BASICS \
	  movement SECT_INSIDE \
	  flags {UNIT_FL_NO_WEATHER, UNIT_FL_INDOORS, UNIT_FL_NO_TELEPORT} \
	  ALWAYS_LIGHT


%zone assassin
title "Asassins Guild@Khorsabad"
lifespan 20
reset RESET_ANYHOW

creators {"Elminster"}

notes
"This is the guild for the assassins based in the arabian city.
Email any comments to jmg3128@tam2000.tamu.edu (Elminster).<br/>
Present caretaker of the guilds can be reached at
otap@dorit.ihi.ku.dk (Skray).<br/>
"

help
"This is the infamous guild of the Khorsabad Assassins, the masters of
quick and silent death. Welcome.<br/>"


%dil

dilbegin nokill();
var
   pc : unitptr;
   room : unitptr;
code
{
   heartbeat := 3 * PULSE_SEC;

   :start:
   wait(SFB_COM,  self.position == POSITION_FIGHTING );
   pc := self.fighting;
   if (pc.level > 200) goto start;
   if (command("flee"))
      block;

   self.hp := self.max_hp;
   act("At the sounds of battle, black-covered shapes file quietly into the "+
      "room.", A_SOMEONE, pc, self, null, TO_ALL);
   act("A sudden blow in the back of your head sends you spinning into"+
      "the murky depths of unconsciousness!",A_SOMEONE,pc,self,null,TO_CHAR);
   act("One of them delivers a sharp blow in the back of $1s head, sending "+
      "$1m down to the ground!", A_SOMEONE,pc,self,null,TO_REST);
   pc.hp := 0;
   act("You feel yourself being dragged away somewhere..."
      ,A_SOMEONE, pc, self, null, TO_CHAR);
   position_update(pc);
   act("Two assassins grab $1n and drag him out of the Guild, dumping "+
      "$1m in the streets.",A_SOMEONE, pc, self, null, TO_REST);
   room := findroom("a65@arabian");
   link(pc,room);
   act("Two dark-clad shapes appear, dragging an unconscious figure between "+
      "them and dump $1m unceremoniously on the street."
      ,A_SOMEONE,pc,self,null,TO_REST);
:gone:
   unsecure(pc);
   goto start;
}
dilend



%rooms
/* ---------------------------- GUILD HALL ------------------------------- */

     entrance
names {"foyer","entrance","assassin guild"}
title "Foyer of the Assassin's Guild"
descr
"   The entire room seems filled with shadow despite many sources of light.
You wonder if the guild is still in operation as no one seems to occupy the
place.  Doorways lead in all directions, including west back out on to the
street."
ROOM_BASICS
flags {UNIT_FL_NO_WEATHER, UNIT_FL_INDOORS,UNIT_FL_NO_TELEPORT}
west to a65@arabian keyword {"wooden door", "door"};
east to hallway_1;
north to skill_room descr "A dark room.";
south to weapon_room descr "A dark room.";
end

    hallway_1
title "Hallway"
descr
"   This hallway extends east from the foyer.  The dark passage has doorways
leading to rooms to the north and south, an additional doorway leads back west
into the foyer.  The hallway continues to the east."
ROOM_BASICS
west to entrance;
north to ability_room descr "A dark room.";
south to spell_room descr "A dark room.";
east to hallway_2;
end

		hallway_2
title "Hallway"
descr
"  The hallway leads back west, but ends in a wall to the east.  The only
doorway leading from this darkened hall is to the north."
ROOM_BASICS
west to hallway_1;
north to master_room descr "A dark room.";
end

		skill_room
title "The Skill Training Room"
descr
"   A stuffed dummy stands prominently in the center of the shadowed room.
Various other devices fill the room, most seemingly to have some use in
honing an assassin's skills.  A doorway leads south back into the foyer."
extra {"stuffed dummy","dummy"}
"The stuffed dummy is covered with many small bells and is slitted in many
places."
ROOM_BASICS
south to entrance;
end

		weapon_room
title "The Weapon Training Room"
descr
"   Among this shadowed room lies racks of weapons that an assassin would
use.  Charts showing vital parts of many varied monsters show the best
place to place the killing dagger.  Stuffed dummies are marked up with
practice blows."
ROOM_BASICS
north to entrance;
end

		master_room
title "The Guild Master's Room"
descr
"   Shadows fill the corners of this room, providing ample places for someone
to hide.  A desk stands near the north wall, but is currently free of any
adornments. A sign hangs on the wall."

extra {"sign"}
"This is the great hall of the Barbarians Guild. <br/><pre>
'join'           - join this guild (if you qualify!).
'title'          - changes your title to a new and better one.
'worship'        - ... ?
'say specialize' - To change profession (if you qualify).
</pre><br/>"

ROOM_BASICS
south to hallway_2;
end

		spell_room
title "The Training Room of Special Skills"
descr
"   Strange devices fill this room.  Judging from what you see, you guess this
is where assassins train their special skills."
ROOM_BASICS
north to hallway_1;
end

		ability_room
title "Ability Training Room"
descr
"   Various tools fill this room.  There are weights, parallel bars, good
books, and mirrors with makeup.  You wonder if this room is used to train
assassins in their various ability scores.  This room, like all the others,
is dark."
ROOM_BASICS
south to hallway_1;
end

     back_room
names {"back room"}
title "A hidden back room of the Assassin's Guild"
descr
"   Well, I suppose you want something to actually be described here, huh?"
ROOM_BASICS
end


%mobiles
/* ----------------------------- ASSASSINS ------------------------------- */

		master_trainer
names {"ja'afar", "master", "assassin"}
title "the Master Assassin"
descr
"The black-robed Master Assassin can barely be discerned from the shadows."
extra {}
"The Master stays nearly hidden within the shadows.  His cold eyes carefully
watch your movements for any signs of deception."
M_HUMAN_WARRIOR_SWORD(150,SEX_MALE)
exp -100

dilcopy nokill@assassin();
dilcopy MurderBanish@guilds(GUILD_ASSASSIN);
dilcopy AlertMembers@guilds(GUILD_ASSASSIN, "");

dilcopy Specialize@guilds(GUILD_ASSASSIN, "alespec@assassin",
    { "say $1N, it is important for you to understand, that specialization is an irreversible process.", 
      "say Once you commit this process will alter your body permanently.",
      "say I'll give you a mug of grey ale. Once you cosume it the change will occur.",
      "say Remember, this will only work for thieves.",
      "::$3n hands you a mug of grey ale.::$3n hands a mug of grey ale to $1n."});

dilcopy MasterJoin@guilds(GUILD_ASSASSIN, GUILD_ASSASSIN_EXCL, QUEST_ASSASSIN_ENTRY, 
 {"say You have been banned from our guild, you can't rejoin.","spit $1N"},
 {"eye $1N", "say Perhaps you have been hitting your head too much lately, $1N?", "say You're already a member of our guild."},
 {"glare $1N", "say You have made some unfortunate choices in life $1N, that prevent you from joining our guild."},
 {"say To join $1N, you must prove yourself worthy by completing the Assassin Guild quest.","say Go speak with Sharki."},
 {"handshake $1N", "say Welcome to the Assassin's Guild, $1N."});


dilbegin guilditem();
external
   integer IsInGuild@guilds(pc : unitptr, sGuild : string);
   integer IsInGuilds@guilds(pc : unitptr, sGuilds : stringlist);
   integer IsBanished@guilds(pc : unitptr, sGuild : string);
   JoinGuild@guilds(pc : unitptr, sGuild : string);
   LeaveGuild@guilds(pc : unitptr, sGuild : string);
   BanishGuild@guilds(pc : unitptr, sGuild : string);
var
   pc:   unitptr;
   item: unitptr;
   expd : extraptr;
code
{
   :init:
   heartbeat := PULSE_SEC*4;

   :start:
   wait (SFB_DONE, command ("worship") and (self ==target));
   pc := activator;
   secure(pc, lostpc);

:stuff:
   if (IsInGuild@guilds(activator, GUILD_ASSASSIN))
   {
      secure(pc,lostpc);
      exec("say Do you feel that you have been a good assassin lately?",self);
      pause;
      exec("say Well, you obviously haven't been using the proper weapon.",self);
      pause;
      exec("say take this, if this can't make you kill better, nothing will. " +
         " go now, and may all your kills be swift.", self);
      pause;
      exec("say and also take this, it will show your membership as an " +
         "Assassin.  Wear it proudly.", self);
      act(" The master Assassin pulls some items from underneath the folds of " +
         " his cloak and gives it to you.",A_ALWAYS,self,null,null,TO_CHAR);
      item := load ("death_bringer@assassin");
      item.height := pc.height;
      link(item,pc);
      item := load ("dark_talisman@assassin");
      item.height := pc.height;
      link(item,pc);
      unsecure(pc);
      goto start;
   }
   else
   {
	   unsecure(pc);
      pause;
      exec("say Beat it or I will have you flogged.", self);
      exec("emote spits on your boots.",self);
   }
   unsecure(pc);
   goto start;

:lostpc:
   unsecure(pc);
   exec("shrug", self);
   goto start;
}
dilend


dilcopy SetTitles@guilds(GUILD_ASSASSIN, {"Novice Cutthroat",
"Novice Cutthroat", "Cutthroat", "Cutthroat", "Master Cutthroat",
"Master Cutthroat", "Novice Hitman", "Novice Hitman", "Hitman", "Hitman",
"Master Hitman", "Master Hitman", "Hidden Blade", "Hidden Blade", "Quick Blade",
"Quick Blade", "Shadow Blade", "Shadow Blade", "Lord of the Shadows",
"Lady of the Shadows", "Silent Strike", "Silent Strike", "Quick Strike",
"Quick Strike", "Death Strike", "Death Strike", "Novice Immolator",
"Novice Immolator", "Immolator", "Immolator", "Master Immolator",
"Master Immolator", "Novice Slayer", "Novice Slayer", "Slayer", "Slayer",
"Master Slayer", "Master Slayer", "Assassin", "Assassin", "True Assassin",
"True Assassin", "Master Assassin", "Master Assassin", "Ultimate Quick Blade",
"Ultimate Quick Blade"});
end

		weapon_trainer
names {"Khaveen", "assassin","teacher","master"}
title "Khaveen"
descr "Khaveen is here to teach you the finer points of weapon use."
extra {}
"You realize that you are standing before one of the greatest masters of
weapon use on the entire continent."
M_HUMAN_WARRIOR_SWORD(150,SEX_MALE)
exp -100

dilcopy nokill@assassin();
dilcopy MurderBanish@guilds(GUILD_ASSASSIN);
dilcopy AlertMembers@guilds(GUILD_ASSASSIN, "");
dilcopy TrainMemberChk@guilds(GUILD_ASSASSIN, "");

#define _WEAPON
#include "guilddef.h"
#undef _WEAPON
dilcopy nokill@assassin();
end


		spell_trainer
names {"Aziza","assassin","teacher","woman"}
title "Aziza"
descr "Aziza is here to teach the special skills of the assassins."
extra {}
"One of the most famous women assassins of all ages will assist you in your
training..."
M_HUMAN_WARRIOR_SWORD(150,SEX_MALE)
exp -100
sex SEX_FEMALE

dilcopy nokill@assassin();
dilcopy MurderBanish@guilds(GUILD_ASSASSIN);
dilcopy AlertMembers@guilds(GUILD_ASSASSIN, "");
dilcopy TrainMemberChk@guilds(GUILD_ASSASSIN, "");

#define _SPELL
#include "guilddef.h"
#undef _SPELL
dilcopy nokill@assassin();

end

		skill_trainer
names {"Ferrari","assassin","teacher"}
title "Ferrari"
descr "Ferrari stands here to teach you the tricks of the trade."
extra {}
"Altough slightly ageing, he is still one of the most infamous assassins
of all ages."

M_HUMAN_WARRIOR_SWORD(150,SEX_MALE)
exp -100

dilcopy nokill@assassin();
dilcopy MurderBanish@guilds(GUILD_ASSASSIN);
dilcopy AlertMembers@guilds(GUILD_ASSASSIN, "");
dilcopy TrainMemberChk@guilds(GUILD_ASSASSIN, "");

#define _SKILL
#include "guilddef.h"
#undef _SKILL
dilcopy nokill@assassin();

end


		ability_trainer
names {"Sharki","assassin","trainer","teacher"}
title "Sharki"
descr "Sharki stands here waiting to aid your advancement of abilities."
M_HUMAN_WARRIOR_SWORD(150,SEX_MALE)
exp -100

dilcopy nokill@assassin();
dilcopy MurderBanish@guilds(GUILD_ASSASSIN);
dilcopy AlertMembers@guilds(GUILD_ASSASSIN, "");
dilcopy TrainMemberChk@guilds(GUILD_ASSASSIN, "");

#define _ABILITY
#include "guilddef.h"
#undef _ABILITY
dilcopy nokill@assassin();

dilcopy qstSimpleQuest@quests(QUEST_ASSASSIN_ENTRY,
         {"say Hey $1N, want to prove yourself worth to our guild?",
          "say All you need to do is to bring me the head of Ploscik from the Clouds."},
         {"say Excellent! Best of luck, $1N."},
         "Sharki in the Assassin's Guild has asked you to bring him the head of Ploscik from the Clouds.",
         {"emote looks at the head of Ploscik.",
          "say Impressive! Well done, $1N. You should go speak with the guild master now."},
          800, 1 * COPPER_MULT, {""});

dilcopy qstItemsWanted@quests(QUEST_ASSASSIN_ENTRY, {"head@death"}, {"decapCheck@quests 0 ploscik@cloud"},
         {"say Excellent, thank you $1N."},
          "say I can't accept that, maybe you could bring me the head of Ploscik from the Clouds?");

end


		assassin_guard
names {"assassin guard","guard","assassin"}
title "an assassin guard"
descr "A vague feeling of being watched washes over you..."
extra {}
"Whoa! If you didn't know he was there, you'd never seen the guy! Clad
all in black, he watches you intently from the shadows."
M_HUMAN_WARRIOR_SWORD(150,SEX_MALE)

dilcopy MurderBanish@guilds(GUILD_ASSASSIN);
dilcopy AlertMembers@guilds(GUILD_ASSASSIN, "");

// Prospects must be able to enter
// dilcopy guild_guard@guild_paths(GUILD_ASSASSIN, "east");
end

		shadow_assassin
names {"shadow assassin", "shadow", "assassin"}
title "the Shadow Assassin"
descr
"The black-robed Shadow Assassin can barely be discerned from the shadows."
extra {}
"The Shadow stays nearly hidden within the shadows.  His cold eyes carefully
watch your movements for any signs of movement."
M_HUMAN_WARRIOR_SWORD(150,SEX_MALE)

dilbegin prg2();
external
   integer IsInGuild@guilds(pc : unitptr, sGuild : string);
var
  pc:   unitptr;
  item: unitptr;
  expd : extraptr;
code
{
   :init:
   heartbeat := PULSE_SEC*4;

   :start:
   wait (SFB_CMD, activator.type == UNIT_ST_PC);
   pc := activator;

   if (not IsInGuild@guilds(pc, GUILD_ASSASSIN))
      goto start;

   secure(pc,lostpc);

   exec("tell "+pc.name+" Hello, fellow assassin.  Would you like to " +
         "journey to our guild to increase your powerful skills?  Just nod if " +
         "you wish to go.", self);
   if (command("nod"))
   {
      act("The Shadow Assassin throws a smoke bomb upon the ground, when the "+
      "smoke clears you find yourself in a different place!"
      ,A_ALWAYS,pc,null,null,TO_CHAR);
      act("The Shadow Assassin throws a smoke bomb upon the ground, when the "+
      "smoke clears, $1n is gone!",A_ALWAYS,pc,null,null,TO_REST);
      link(pc, findroom("entrance@assassin"));
   }

   unsecure(pc);
   :lostpc:
   goto start;
}
dilend
end


		shdw2_assassin

names {"shadow assassin", "shadow", "assassin"}
title "the Shadow Assassin"
descr
"The black-robed Shadow Assassin can barely be discerned from the shadows."
extra {}
"The Shadow stays nearly hidden within the shadows.  His cold eyes carefully
watch your movements for any signs of movement."
M_HUMAN_WARRIOR_SWORD(150,SEX_MALE)

dilbegin prg3();
external
   integer IsInGuild@guilds(pc : unitptr, sGuild : string);
var
  pc:   unitptr;
  item: unitptr;

code
{
  :init:
  heartbeat := PULSE_SEC*4;

  :start:
  wait (SFB_CMD, activator.type == UNIT_ST_PC);
  secure(pc,lostpc);
  pc := activator;

   if (IsInGuild@guilds(activator, GUILD_ASSASSIN))
   {
      exec("say Hello, fellow assassin.  Would you like a lift back to midgaard? " +
         "Just nod if you wish to go.", self);

      if (command("nod"))
      {
         act("The Shadow Assassin throws a smoke bomb upon the ground, when the "+
         "smoke clears you find yourself in a different place!"
         ,A_ALWAYS,pc,null,null,TO_CHAR);
         act("The Shadow Assassin throws a smoke bomb upon the ground, when the "+
         "smoke clears, $1n is gone!",A_ALWAYS,pc,null,null,TO_REST);
         link(pc, findroom("market_sq@midgaard"));
      }
   }

   unsecure(pc);

   :lostpc:
   goto start;
}
dilend

end

%objects
/* ------------------------ LIST ------------------------------ */

		captain_dagger
names {"dagger"}
title "a dagger"
descr "A sleek dagger lies here."
extra {}
"The dagger feels like it is meant to kill on specific target but is
otherwise normal."
extra {"$identify"}
"This dagger is designed to kill the Amir in Khorsabad.
Backstab the Amir to accomplish your mission.<br/>"
manipulate {MANIPULATE_TAKE, MANIPULATE_WIELD}
WEAPON_DEF(WPN_DAGGER, BONUS_AVERAGE_PLUS, BONUS_AVERAGE_PLUS)  // Xyzzy: 1,1
SKILL_TRANSFER(SKI_BACKSTAB, BONUS_GOOD_PLUS)  // Xyzzy: 3
weight 1

dilbegin prg4();
var
  pc:   unitptr;
  opp:  unitptr;
code
{
  :start:
  wait (SFB_CMD, (activator.type == UNIT_ST_PC) and
		 (self.equip == WEAR_WIELD) and
		 (command("backstab")));
  if (not(ASSASSIN_ONGOING in activator.quests))
    goto start;
  opp := findunit(activator, argument, FIND_UNIT_SURRO, null);
  if (not((opp.nameidx == "amir1") and (opp.zoneidx == "arabian")))
    goto start;
  pc := activator;
  secure(pc, start);

  :loop:
  wait(SFB_COM, TRUE);
  if ((pc.fighting.nameidx == "amir1") and
      (pc.fighting.zoneidx == "arabian"))
  {
    act("Blood pours out from the wound in the $3N's back.",
	A_HIDEINV, pc, null, pc.fighting, TO_CHAR);
    pc.fighting.hp := 1;
    subextra(pc.quests, ASSASSIN_ONGOING);
    addextra(pc.quests, {ASSASSIN_COMPLETE},"");
    destroy(self);
  }
  else
    goto loop;
}
dilend
end

			dark_talisman
names {"Black Lotus Talisman","talisman","lotus"}
title "a Black Lotus Talisman"
descr "A small talisman in the shape of a black lotus flower lies here."
extra {}
"This dark talisman has been used by assassins for years to show their
membership in the assassin's guild. The black lotus flower is a talisman
of good luck."
extra {"$identify"}
"This talisman can only be worn by assassins and will increase their
agility.<br/>"
extra {"$improved identify"}
"The bonus to Dex is +1. A dexterity of 20 is required to wear this
talisman.<br/>"
manipulate {MANIPULATE_TAKE, MANIPULATE_WEAR_NECK}
flags {UNIT_FL_MAGIC}
type ITEM_WORN
DEX_TRANSFER(BONUS_AVERAGE_PLUS)  // Xyzzy: +1
weight 0
cost 1 IRON_PIECE
rent 50 IRON_PIECE
dilcopy guild_restrict@function({GUILD_ASSASSIN},0,25,"");
dilcopy abi_restrict@function(ABIL_DEX,20,0,0,"");
end



				death_bringer
names {"Death Bringer","shortsword","sword","bringer"}
title "Death Bringer"
descr "A black, sleek shortsword lies here."
extra {}
"This sleek, elegant weapon is perfectly balanced and razor sharp, perfect
for back stabbing! Engraved upon its side are the words 'Death Bringer',
written in arabic."
extra {"engraving"}
"The arabic letters spell 'Death Bringer'.<br/>"
extra {"$identify"}
"This powerful weapon will drain the health of your opponent and transfer
it to you..<br/>"
extra {"$improved identify"}
"This weapon has a craftsmanship and magical modifier of 7. It improves
the wearers dexterity and magical power by two, and reduces the power of divine 
interactions by 1. Skill in short sword will be improved by 5."
WEAPON_DEF(WPN_SHORT_SWORD, BONUS_EXCELLENT, BONUS_EXCELLENT)  // Xyzzy: 7,7
DEX_TRANSFER(BONUS_GOOD)  // Xyzzy: +2
MAG_TRANSFER(BONUS_GOOD)  // Xyzzy: +2
DIV_TRANSFER(BONUS_AVERAGE_MINUS)  // Xyzzy: -1
WEAPON_TRANSFER(WPN_SHORT_SWORD, BONUS_EXCELLENT_PLUS)  // Xyzzy: +5
manipulate {MANIPULATE_TAKE, MANIPULATE_WIELD}
weight 4
cost 1 IRON_PIECE
rent 150 IRON_PIECE
dilcopy guild_restrict@function({GUILD_ASSASSIN},0,25,"");
dilcopy abi_restrict@function(ABIL_DEX,20,0,0,"");


dilbegin death();
external
   integer IsInGuild@guilds(pc : unitptr, sGuild : string);

var
   storenum        :       integer;
   num             :       integer;
   num2            :       integer;
   diff            :       integer;
   lev		:	integer;
   mag_skill	:	integer;
   skill_com	:	integer;
   dex_skill       :       integer;
   sho_skill	:	integer;

code
{
   :init:
   heartbeat := PULSE_SEC*4;

   :start:
   wait(SFB_COM, ((self.outside.fighting) and
       (self.equip == WEAR_WIELD)));

   if (IsInGuild@guilds(self, GUILD_ASSASSIN))
   {
      num2 := rnd(1, 5);
      storenum := self.outside.fighting.max_hp / (4 * num2);
      sho_skill := self.outside.weapons[WPN_SHORT_SWORD];
      mag_skill := self.outside.abilities[ABIL_MAG];
      dex_skill := self.outside.abilities[ABIL_DEX];
      skill_com := ((sho_skill + mag_skill + dex_skill) / 3);
      num2 := rnd(0, 99);
      if ((skill_com*num2)/100 >= 25)
      {
         num := rnd(0, 99);
        lev := self.outside.level;
	if ((lev*num)/100 >= 15)
         {
            act("Your sword glows as it absorbs your opponents life energy!",
                A_ALWAYS, self.outside, null, null,TO_CHAR);
	    act(""+self.outside.name+"'s Death Dealer glows as it steals " +
	    "the life force from "+self.outside.fighting.name+".", A_ALWAYS,
	    self.outside, null, null, TO_REST);
         }
         else
             pause;
             goto start;

    }
}
}
dilend

end

		master_cape
names {"black cape","cape"}
title "a black velvet cape"
descr "A black cape of soft velvet lies here."
extra {}
"A beautiful and soft cape of black velvet."
manipulate {MANIPULATE_TAKE,MANIPULATE_WEAR_ABOUT}
ARMOUR_CLOTHES(BONUS_AVERAGE, BONUS_AVERAGE)  // Xyzzy: 0,0
weight 1
rent 60 IRON_PIECE
dilcopy abi_restrict@function(ABIL_CHA,50,0,0,"cha_rest@skaven");
end

		bokken
names {"bokken","katana","sword","replica"}
title "a bokken sword"
descr "A wooden replica of a katana sword lies here."
extra {}
"A wooden replica of a katana sword, used for training purposes only."
manipulate {MANIPULATE_TAKE,MANIPULATE_WIELD}
WEAPON_DEF(WPN_SCIMITAR, BONUS_BAD_MINUS, BONUS_AVERAGE)  // Xyzzy: -3,0
cost 1 COPPER_PIECE
weight 2
end

		dark_katana
names {"dark-bladed katana","katana","sword"}
title "a dark-bladed katana"
descr "A dark-bladed katana sword lies here."
extra {}
"The blade of this katana has been covered in some dark, oily substance,
making it black and mate. The handle bears the sigil of the Assassin Guild."
extra {"$identify"}
"This katana will raise your Dexterity as well as your katana skills and
hide skills.<br/>"
extra {"$improved identify"}
"The katana and hide skills will raise with 3%, Dexterity with 2 points.<br/>
The katana is a weapon of craftmanship 5 and magic bonus 5.<br/>"
manipulate {MANIPULATE_TAKE,MANIPULATE_WIELD}
WEAPON_DEF(WPN_SCIMITAR, BONUS_AVERAGE, BONUS_AVERAGE)  // Xyzzy: 0,0
weight 2
cost 3 GOLD_PIECE
rent 68 IRON_PIECE
dilcopy guild_restrict@function({GUILD_ASSASSIN},0,10,"");
dilcopy abi_restrict@function(ABIL_DEX,70,0,0,"dex_rest@skaven");
end

		dark_wakiz
names {"dark-bladed wakizashi","wakizashi","sword"}
title "a dark-bladed wakizashi"
descr "A dark-bladed wakizashi sword lies here."
extra {}
"The blade of this wakizashi has been covered in some dark, oily substance,
making it black and mate. The handle bears the sigil of the Assassin Guild."
extra {"$identify"}
"This wakizashi will raise your Dexterity as well as your wakizashi and
hide skills.<br/>"
extra {"$improved identify"}
"The wakizashi and hide skills will raise with 3%, Dexterity with 2 points.<br/>
The wakizashi is a weapon of craftmanship 5 and magic bonus 5.<br/>"
manipulate {MANIPULATE_TAKE,MANIPULATE_WIELD}
WEAPON_DEF(WPN_SCIMITAR, BONUS_AVERAGE, BONUS_AVERAGE)  // Xyzzy: 0,0
weight 1
cost 3 GOLD_PIECE
rent 68 IRON_PIECE
dilcopy guild_restrict@function({GUILD_ASSASSIN},0,10,"");
dilcopy abi_restrict@function(ABIL_DEX,70,0,0,"dex_rest@skaven");
end


alespec
names {"mug of grey ale","mug", "grey ale", "ale"}
title "a mug of ale"
descr "A wooden mug stands here."
extra {}
"A large, wooden mug."
LIQ_DEF("grey", 0,1,1,5,2,5,0)
manipulate {MANIPULATE_TAKE,MANIPULATE_HOLD}
cost 3 IRON_PIECE
dilcopy Transform@guilds(PROFESSION_THIEF, PROFESSION_ASSASSIN, 
   {"::The grey ale flows down your throat.::",
    "::You get a warm feeling.::",
    "::You feel more sneaky::",
    "::You're now an Assassin::"});
end

%reset

load assassin_guard into a65@arabian max 1
{
        equip dark_talisman                     WEAR_NECK_1
        equip outxiyang/ninja_sleeves           WEAR_ARMS
        equip outxiyang/ninja_trousers          WEAR_LEGS
        equip outxiyang/ninja_tun             WEAR_BODY
        equip outxiyang/ninja_shoes             WEAR_FEET
	    equip dark_wakiz			WEAR_HOLD
        equip dark_katana                       WEAR_WIELD
        equip outxiyang/wristguard              WEAR_WRIST_R
        equip outxiyang/wristguard              WEAR_WRIST_L
}

load master_trainer into master_room max 1
{
	equip dark_talisman			WEAR_NECK_1
	equip outxiyang/ninja_sleeves		WEAR_ARMS
	equip outxiyang/ninja_trousers		WEAR_LEGS
	equip outxiyang/ninja_tun		WEAR_BODY
	equip outxiyang/ninja_shoes		WEAR_FEET
	equip outxiyang/ninjanto		WEAR_WIELD
	equip outxiyang/wristguard		WEAR_WRIST_L
	equip outxiyang/wristguard		WEAR_WRIST_R
	equip master_cape			WEAR_ABOUT
}

load skill_trainer into skill_room max 1
{
	equip dark_talisman			WEAR_NECK_1
	equip master_cape			WEAR_ABOUT
	equip outxiyang/ninja_sleeves		WEAR_ARMS
	equip outxiyang/ninja_trousers		WEAR_LEGS
	equip outxiyang/ninja_tun		WEAR_BODY
	equip death_bringer			WEAR_WIELD
}

load weapon_trainer into weapon_room max 1
{
	equip dark_talisman			WEAR_NECK_1
	equip master_cape			WEAR_ABOUT
	equip outxiyang/ninja_sleeves		WEAR_ARMS
	equip outxiyang/ninja_trousers		WEAR_LEGS
	equip outxiyang/ninja_tun		WEAR_BODY
	equip bokken				WEAR_WIELD
}

load spell_trainer into spell_room max 1
{
	equip dark_talisman			WEAR_NECK_1
	equip master_cape			WEAR_ABOUT
	equip outxiyang/ninja_sleeves		WEAR_ARMS
	equip outxiyang/ninja_trousers		WEAR_LEGS
	equip outxiyang/ninja_tun		WEAR_BODY
	equip arabian/wakizashi			WEAR_WIELD
}

load ability_trainer into ability_room max 1
{
	equip dark_talisman			WEAR_NECK_1
	equip master_cape			WEAR_ABOUT
	equip outxiyang/ninja_sleeves		WEAR_ARMS
	equip outxiyang/ninja_trousers		WEAR_LEGS
	equip outxiyang/ninja_tun		WEAR_BODY
	equip dark_katana			WEAR_WIELD
}

load shadow_assassin into royal_bridge@midgaard max 1
{
        equip dark_talisman                     WEAR_NECK_1
        equip outxiyang/ninja_sleeves           WEAR_ARMS
        equip outxiyang/ninja_trousers          WEAR_LEGS
        equip outxiyang/ninja_tun             WEAR_BODY
        equip outxiyang/ninja_shoes             WEAR_FEET
        equip arabian/wakizashi                	WEAR_HOLD
	equip dark_katana			WEAR_WIELD
        equip outxiyang/wristguard              WEAR_WRIST_R
}
load shdw2_assassin into entrance@assassin max 1
{
        equip dark_talisman                     WEAR_NECK_1
        equip outxiyang/ninja_sleeves           WEAR_ARMS
        equip outxiyang/ninja_trousers          WEAR_LEGS
        equip outxiyang/ninja_tun             WEAR_BODY
        equip outxiyang/ninja_shoes             WEAR_FEET
        equip arabian/wakizashi                	WEAR_HOLD
	equip dark_katana			WEAR_WIELD
        equip outxiyang/wristguard              WEAR_WRIST_L
}


%end
