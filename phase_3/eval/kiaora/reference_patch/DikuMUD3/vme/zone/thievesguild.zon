/*
filename    thievesguild
password    newworld
changedby   drevar
EmailAdd     Whistler <whistler@blinksoft.com>
request     compile
version     19
END HEADER*/

/* This zone is the Thieves Guild. It has been made as                   */
/* a separate zone in order to make Midgaard smaller.                    */
/* seifert@diku.dk 24/02/93                                              */

/* For use with the teacher-include file! */
#define _GUILD_UDGAARD_THIEF
#define THIEVES_DIRK "$Thieves Dirk given"
#define QUEST_THIEVES_ENTRY "Thieves Guild Entry"


#include <macros.h>

/* Training value sets (USE THESE). Special permission required to
   use different value sets.

   Very Cheap     :   5, 10, 20, 30,  40
   Cheap          :   8, 16, 24, 32,  40
   Normal         :  10, 20, 30, 40,  50
   Expensive      :  15, 30, 45, 60,  75
   Very Expensive :  20, 40, 60, 80, 100
*/

/* ================================================================== */
/*                                ZONE                                */
/* ================================================================== */
%zone thievesguild
title "Thieves GuildMainland"
creators {"redferne"}

notes
"This is a zone designed to implement the Guild for Thieves. The zone is
not a part of Midgaard, since it is too large already. The zone
contains teachers and guards. Lifespan of zone is very high in order
to encourage members to defend their guild.<br/>
Version 1.0 made 21/2-93 by Redferne (Michael Curran).<br/>
Juni 4th, 1996 - Thieves' Dirk added as the guild item by Sk(r)ay.<br/>"

help
"<pre> 1 2 3 4 5 6<br/>
<br/>
A     #        A3 Skill Teacher<br/>
      S        B4 Ability Teacher<br/>
B     #-#      C2 Guild Master<br/>
     /|        D2 Weapon Teacher<br/>
C   # |        D4 Bar Tender<br/>
      |<br/>
D   #-#-# #-@  D6 Guild road<br/>
      |  /<br/>
E     #--<br/>
<br/>
This is the Midgaard Thieves Guild. This Guild<br/>
specializes in training dexterity and stealth as well as thieving skills.<br/></pre>"

reset RESET_ANYHOW
lifespan 129

/* ================================================================== */
/*                               DIL                                  */
/* ================================================================== */

%dil

/* Nokill dill for Guild Staff - ejects the sinner out with 0 hp and
endurance + incapacitated and with some cash loss...*/

dilbegin nokill();
var
   pc 	: unitptr;
   room 	: unitptr;
	i	: integer;
code
{
   heartbeat := 3 * PULSE_SEC;

   :start:
   wait(SFB_COM,  self.position == POSITION_FIGHTING );
   pc := self.fighting;
   if (pc.level >200) goto start;
   secure(pc,gone);
   if (command("flee"))
         block;

   self.hp := self.max_hp;
   act("The sounds of battle attract the attention of the thieves.",
   A_ALWAYS, pc, self, null, TO_ALL);
   act("Suddenly, several large thugs charge into the room, beating you "+
   "into a bloody pulp!", A_ALWAYS, pc, self, null, TO_CHAR);
   act("Several large thugs charge into the room, beating $3n into a bloody "+
   "pulp!",A_SOMEONE, pc, self, null, TO_REST);
   pc.hp := 0;
   pc.endurance := 0;
   act("Half-conscious, you can feel hands going through your pockets as "+
   "you are dragged away...",A_ALWAYS, pc, self, null, TO_CHAR);
   position_update(pc);
   i:=transfermoney(pc,null,(rnd(1,100)*COPPER_MULT));
   act("Two of the thugs grab $1n and drag $1m out of the Guild, dumping "+
   "$1m in the streets.",A_SOMEONE, pc, self, null, TO_REST);
   room := findroom("grub_barrel@midgaard");
   link(pc,room);
   act("Two thugs appear, dragging an unconscious figure between them and "+
   "dump $1m unceremoniously on the street, giving $1m a kick as they leave."
   ,A_SOMEONE,pc,self,null,TO_REST);

:gone:
   unsecure(pc);
   goto start;
}
dilend


/* ================================================================== */
/*                               ROOMS                                */
/* ================================================================== */

%rooms


                                entrance

title "The Thieves Bar"
descr
"    This is an abandoned bar which has been left unused for many years.
There is a sign hanging on the wall. There is a dark alley to the north.
An old wooden staircase leads down into the wine cellar."
flags {UNIT_FL_NO_WEATHER, UNIT_FL_INDOORS,UNIT_FL_NO_TELEPORT}
movement SECT_INSIDE
north to grub_barrel@midgaard;
down to wcellar2;
ALWAYS_LIGHT
extra {"sign", "wall"}
"The sign says:<pre><br/>
<br/>
   This place is out of business.<br/>
<br/>
</pre>"
end



                                wcellar2

names{"wine cellar"}
title "The Wine Cellar"
descr
"    This room seems a little damp, but not nearly as much as the upper
wine cellar. The two dozen beer barrels seem to be of fine quality compared
to the cheap slosh they sell in the bar of the Grubby Inn. The dozen barrels
of wine look to be of greater quality too."
flags {UNIT_FL_NO_WEATHER, UNIT_FL_INDOORS,UNIT_FL_NO_TELEPORT}
movement SECT_INSIDE
north to wcellar3;
up to entrance;
ALWAYS_LIGHT
end


                               wcellar3

names{"middle wine cellar"}
title "The Guild of Thieves Entrance"
descr
"    A faint scent of wine fills the air here and slight sounds of music
floats in from the door to the east. A low door leads to the adjoining
lower wine cellar to the south. Over a large oak door to the west a sign
says 'Coals' and underneath that a small paper says 'Master of Weapons'.
Over the opening to the north a large piece of parchment says 'The Thieving
Guiltae'."
flags {UNIT_FL_NO_WEATHER, UNIT_FL_INDOORS,UNIT_FL_NO_TELEPORT}
movement SECT_INSIDE
west to coal_room open {EX_OPEN_CLOSE, EX_CLOSED}
     keyword {"door","oak door","oak"};
east to thieves_bar;
south to wcellar2;
north to wcellar4;
ALWAYS_LIGHT
end


                                thieves_bar

names {"thieves bar"}
title "Smugglers Corner"
descr
"    This low-ceilinged inn has a comforting air to it that relaxes you
in addition to the soft sounds of music emanating from a large adorned
musical box that takes up space in one corner. You really feel welcome here.
A low counter along the north wall functions as a bar. An opening in
the west wall leads out into a wine cellar.<br/>
A small sign catches your attention."
extra {"small sign","sign"}
"<pre>The sign says:<br/>
<br/>
	'list'		- to check the current drinks.<br/>
	'buy &lt;drink&gt;'	- to name yer poison.<br/>
<br/>
	   	 Absolutely NO credit!!!!!<br/>
		  No brawls in the bar!!!<br/>
<br/>
</pre>"
extra {"musical box","box"}
"It looks as if it's at least fifty years old, made completely out of
pine wood.<br/>"
flags {UNIT_FL_NO_WEATHER, UNIT_FL_INDOORS,UNIT_FL_NO_TELEPORT,UNIT_FL_SACRED}
movement SECT_INSIDE
west to wcellar3;
ALWAYS_LIGHT
end



                                coal_room

names {"coal room"}
title "Training Centre and Coal Room"
descr
"    The floor in here is completely covered with coal and coal dust. A
small flagstone stands up right against the southern wall. The northern
wall is mainly an enormous mound of coals."
flags {UNIT_FL_NO_WEATHER, UNIT_FL_INDOORS,UNIT_FL_NO_TELEPORT}
movement SECT_INSIDE
east to wcellar3 open {EX_OPEN_CLOSE, EX_CLOSED}
     keyword {"door","oak door","oak"};
ALWAYS_LIGHT
end



                                wcellar4

names {"inner wine cellar"}
title "The Inner Wine cellar"
descr
"    The walls are carved from the virgin rock underneath the city. The
wall to the north has a near-invisible crack. A low staircase disappears
down into darkness in the middle of the room. A large opening leads south
into a large wine cellar much resembling this one. The absence of barrels
in this wine cellar seems a little out of place. A low opening in the east
wall apparently leads to some sort of storage room. Over the opening a
sign says 'No pain, No gain'."
extra {"north wall", "wall", "crack"}
"  The crack forms a line running from floor to floor in an arc that COULD
resemble a door."

flags {UNIT_FL_NO_WEATHER, UNIT_FL_INDOORS,UNIT_FL_NO_TELEPORT}
movement SECT_INSIDE
north to work_shop open {EX_OPEN_CLOSE, EX_CLOSED, EX_HIDDEN}
      keyword {"secret door", "door", "wall", "crack", "north wall"} difficulty 0;
south to wcellar3;
east to storage_room;
down to study;
ALWAYS_LIGHT
end



                                storage_room

names {"storage room"}
title "The Storage Room"
descr
"    A strong pong of hardship and sweaty labour pervades the entire room.
Large boulders cover the west end of the room, and wooden figures adorn
the north end. Ropes are strung out from wall to wall, apparently for
tight rope walking. A low opening leads west into a wine cellar."
extra {"figures","wooden figures","wooden"}
"    These figures look as if they have suffered the training of the knife,
fighting these type of figures give a good practice in dexterity, as they
turn at a stroke and seem to attack from behind."
flags {UNIT_FL_NO_WEATHER, UNIT_FL_INDOORS,UNIT_FL_NO_TELEPORT}
movement SECT_INSIDE
west to wcellar4;
ALWAYS_LIGHT

/* -----------------  Ability Teacher for thieves -------------- */

end



                                work_shop

names {"the work shop"}
title "The Thieves' Work Shop"
descr
"    This room is completely empty except for a doll of wood and cloth. It os
dressed in fancy clothes and stands in the middle of the room. A heavy door
makes an exit to the south leading out to a wine cellar."
extra {"doll","cloth doll","clothes"}
"    The dress of the doll is of high quality silk, which only the richest
of Midgaard can afford. Interwoven in the fabric are hundreds of tiny
brass bells that loudly proclaim almost any touch upon the doll."
flags {UNIT_FL_NO_WEATHER, UNIT_FL_INDOORS,UNIT_FL_NO_TELEPORT}
movement SECT_INSIDE
south to wcellar4 open {EX_OPEN_CLOSE, EX_CLOSED}
      keyword {"door","stone door"};
ALWAYS_LIGHT

/* ------------------  Skills Teacher for Thieves ----------------- */

end



                                study

names {"the study"}
title "Guildmasters Study"
descr
"    Paintings of great thieves and renowned criminals adorn the walls of
this place. The lighting is low but comfortable, warmth emanates from a
large fireplace in the northern wall. A staircase leads up into a cool
wine cellar. There is a big sign on the wall."
flags {UNIT_FL_NO_WEATHER, UNIT_FL_INDOORS,UNIT_FL_NO_TELEPORT}
movement SECT_INSIDE
up to wcellar4;
ALWAYS_LIGHT

extra {"sign", "big sign"}
"<pre>                            The Thieves Guild<br/>
<br/>
   'join'           - join this guild (if you qualify!).
   'title'          - changes your title to a new and better one.
   'worship'        - ... 
<br/>
  All current members - show respect to your guild master by worshipping<br/>
  him or bowing to him and perhaps he will care to share a bit of the<br/>
  profit with you.<br/>
<br/>
</pre>"

/* -------------------  The Thieves GuildMaster ------------------- */

end

/* ================================================================== */
/*                              MOBILES                               */
/* ================================================================== */

%mobiles

                              oliver
names {"oliver", "guild master", "master"}
title "Oliver the Guild Master"
descr "Oliver the Guild Master is here sitting behind his desk."
extra {}
"This elderly thief is one of the best in his trade. Although his hair is
almost totally gray, his green eyes shine as sharply as they ever did."
flags {UNIT_FL_NO_TELEPORT}
romflags {CHAR_PROTECTED}
M_HUMAN_THIEF_SWORD(90, SEX_MALE)
alignment 1000
money 2 IRON_PIECE
exp -100

dilcopy nokill@thievesguild();
dilcopy MurderBanish@guilds(GUILD_UDG_THIEF);
dilcopy AlertMembers@guilds(GUILD_UDG_THIEF, "");

dilcopy SetTitles@guilds(GUILD_UDG_THIEF, {"Pilferer", "Pilferers",
"Footpad", "Footpad", "Filcher", "Filcheress", "Pick-Pocket", "Pick-Pocket",
"Sneak", "Sneak", "Pincher", "Pincheress", "Cut-Purse", "Cut-Purse",
"Snatcher", "Snatcheress", "Sharper", "Sharpress", "Rogue", "Rogue", "Robber",
"Robber", "Magsman", "Magswoman", "Highwayman", "Highwaywoman", "Burglar",
"Burglaress", "Thief", "Thief", "Knifer", "Knifer", "Quick-Blade",
"Quick-Blade", "Killer", "Murderess", "Brigand", "Brigand", "Cut-Throat",
"Cut-Throat"});

dilcopy MasterJoin@guilds(GUILD_UDG_THIEF, GUILD_THIEF_EXCL, QUEST_THIEVES_ENTRY, 
 {"say You have been banned from our guild, you can't rejoin.","spit $1N"},
 {"eye $1N", "say Perhaps you have been hitting your head too much lately, $1N?", "say You're already a member of our guild."},
 {"glare $1N", "say You have made some unfortunate choices in life $1N, that prevent you from joining our guild."},
 {"say To join $1N, you must prove yourself worthy by completing the Thieve's Guild quest.","say Go speak with Marie."},
 {"handshake $1N", "say Welcome to the Thieve's Guild, $1N."});


/*  Thieves Master DIL - gives out a Thieves' Dirk. */
dilbegin thieves_give();
external
   integer IsInGuild@guilds(pc : unitptr, sGuild : string);

var
   pc : unitptr;
   expd : extraptr;
   item : unitptr;
code
{
   heartbeat := PULSE_SEC * 5;

:start:
   wait (SFB_DONE, (command ("worship")or(command("bow"))) and(self ==target));
   pc := activator;
   secure(pc, lost);

   if (IsInGuild@guilds(pc, GUILD_UDG_THIEF))
   {
      exec("say Yes, yes, yes, I've seen you, no reason to grovel on the floor.",self);
      pause;
      if (THIEVES_DIRK in pc.quests)
      {
         exec("say Say, I've seen you here before, "+pc.name+".",self);pause;
         exec("say And since I doubt sincerely you come to me because of "+
         "my beautiful green eyes, you must want something.",self);pause;
         exec("say As I recall, I gave you once a Thieves' Dirk, do you wish "+
         "to get another one of me?",self);pause;
         exec("say Tell me, do I have the distinct look of a gift-shop?",self);
         pause;
         exec("say But okay, I will give you another one. If you prove to me "+
         "that you are NOT a miserable excuse for a thief, who loses his weapon."
         ,self);pause;
         exec("emote thinks for a moment.",self);pause;
         exec("say I will make it easy on you. Bring me an amethyst of first "+
         "sorting and I will give you another dirk.",self);pause;
         exec("emote shakes his head and mumbles something about stupid gits."
         ,self);
         goto start;
      }
      exec("say Let's cut straight to business.",self);pause;
      exec("say As you know, our last year was rather prosperous, thanks to our "+
      "devoted and hard-working members.",self);
      exec("wink "+pc.name,self);pause;
      exec("say Thus now I am able to present you with an item of the Guild... "+
      "It will aid you in your... assignments.",self);
      exec("grin "+pc.name,self);pause;
      exec("say Of course, it is a mere toy for the more experienced members, "+
      "but a valuable aid for promising, young urchins.",self);
      exec("nod "+pc.name,self);pause;
      exec("say Here you go... test it yourself.",self);
      item := load("thief_dirk@thievesguild");
      item.height := pc.height;
      link(item, pc);
      sendtext(self.name+" hands you a small dirk.<br/>",pc);
      act("$3n hands $1n a small dirk.",A_SOMEONE,pc,null,self,TO_REST);
      pause;
      exec("say Hope you can use it. Be careful with the poison.",self);pause;
      addextra(pc.quests, {THIEVES_DIRK}, "");
      exec("emote waves goodbye. This conversation is apparently over.",self);
      goto start;
   }
   else
   {
      unsecure(pc);
      exec ("say Scram, bum. Do I look like a gift-shop?", self);
      goto start;
   }

   unsecure(pc);
   goto start;

:lost:
   unsecure(pc);
   exec("shrug", self);
   goto start;
}
dilend


dilbegin dirk_replace();
external
   integer IsInGuild@guilds(pc : unitptr, sGuild : string);

var
   pc : unitptr;
   item : unitptr;
   gift : unitptr;
code
{
   heartbeat := PULSE_SEC * 4;

:start:
   wait (SFB_DONE, (command("give")and(self ==target)));
   pc := activator;
   secure(pc, lost);
   gift := medium;
   secure(pc,lost);

   if (IsInGuild@guilds(pc, GUILD_UDG_THIEF) and (THIEVES_DIRK in pc.quests))
   {
      if(gift.name == "amethyst")
      {
         exec("say Beautiful.",self);pause;
         exec("emote examines the gemstone and hides it in his pocket.",self);
         destroy(gift);pause;
         exec("say Guess you are not such a fluff head after all.",self);
         exec("say Here is your new dirk, now, DON'T lose this one!",self);
         item := load("thief_dirk@thievesguild");
         item.height := pc.height;
         link(item, self);
         exec("give dirk "+pc.name, self);pause;
         exec("say Good, now beat it.",self);
      }
      else
      {
         exec("say Thank you, but bribery will get you nowhere.",self);pause;
         exec("emote examines "+gift.title+" and hides it away.",self);pause;
         exec("emote grins wickedly.",self);
      }
:lost:
      destroy(gift);
      unsecure(pc);
      goto start;
   }
   goto start;
}
dilend


end

				gryma

names {"Gryma","thief","master"}
title "Gryma"
descr "Gryma the Master Thief is standing here."
extra {}
"He looks exactly like any other thieving scoundrel, except he has a rather
professional look about his moves. You never know where his hands are.
Currently they are in your pockets."
flags {UNIT_FL_NO_TELEPORT, CHAR_OUTLAW}

M_HUMAN_THIEF_CLUB(80, SEX_MALE)
alignment 0
exp -100

dilcopy nokill@thievesguild();
dilcopy MurderBanish@guilds(GUILD_UDG_THIEF);
dilcopy AlertMembers@guilds(GUILD_UDG_THIEF, "");
dilcopy TrainMemberChk@guilds(GUILD_UDG_THIEF, "");

#define _SKILL
#include <guilddef.h>
#undef _SKILL

end

                            pirate

names {"pirate"}
title "the pirate"
descr "An experienced retired pirate is here."
extra {}
"A rather scurvy-looking pirate. The word is that he made a fortune in
the southern seas and gambled it all away. No matter what, he is the best
fighter this place has known."
flags {UNIT_FL_NO_TELEPORT}
romflags {CHAR_PROTECTED}
M_HUMAN_THIEF_SWORD(80, SEX_MALE)
alignment 350
money 70 IRON_PIECE
exp -100

dilcopy nokill@thievesguild();
dilcopy MurderBanish@guilds(GUILD_UDG_THIEF);
dilcopy AlertMembers@guilds(GUILD_UDG_THIEF, "");
dilcopy TrainMemberChk@guilds(GUILD_UDG_THIEF, "");

#define _WEAPON
#include <guilddef.h>
#undef _WEAPON
end



				bisse

names {"Bisse","thief"}
title "Bisse the thief"
descr "Bisse the thief is here."
extra {}
"Rather muscular type, this one spent his youth breaking bones on the
streets for money."
flags {UNIT_FL_NO_TELEPORT}
romflags {CHAR_PROTECTED}
M_HUMAN_THIEF_AXE(80, SEX_FEMALE)
alignment 0
money 8 IRON_PIECE
exp -100

dilcopy nokill@thievesguild();
dilcopy MurderBanish@guilds(GUILD_UDG_THIEF);
dilcopy AlertMembers@guilds(GUILD_UDG_THIEF, "");
dilcopy TrainMemberChk@guilds(GUILD_UDG_THIEF, "");

#define _ABILITY
#include <guilddef.h>
#undef _ABILITY
end


                              marie
names {"Marie","bartender"}
title "Marie the bartender"
descr "Marie the bartender is standing behind the counter."
extra {}
"This suprisingly thin girl seems quite out of place here, but you catch
the glimmer in her eye warning you that she can be quite a dangerous foe
with that knife of hers."
M_HUMAN_THIEF_SWORD(50, SEX_FEMALE)
flags {UNIT_FL_NO_TELEPORT}
romflags {CHAR_PROTECTED}
money 8 IRON_PIECE
dilcopy nokill@thievesguild();
dilcopy shopkeeper@function(
{"firebreather@midgaard 20 20","beer_bottle@midgaard 20 20"
,"ale_bottle@midgaard 20 20"
,"brandy_bottle@midgaard 20 20","tuborg@udgaard 20 50"},
 { "$1n says, 'No more right now, $3n.'",
   "$1n says, 'Ye tryin' to pull me leg, $3n?'",
   "$1n says, 'I don't buy stuff, you never know where it's been.'",
   "$1n exclaims, 'No credit, want me to spell it for ya, $3n?!'",
   "$1n exclaims, 'One $2N commin' right up!'",
   "$1n says, 'Excellent, it's a deal, $3n.'",
   "$1n says, 'Thass too much cash fer me, $3n.'",
   "$1n asks, 'I'm having a quick lunch, gimme a break.'" },
{},"",100,10,2*PLATINUM_MULT,"",""
);


dilcopy qstSimpleQuest@quests(QUEST_THIEVES_ENTRY,
         {"say Hey $1N, want to prove yourself worth to our guild?",
          "say All you need to do is to bring me the head of Shaun the guard from Midgaard."},
         {"say Excellent! Best of luck, $1N."},
         "Maria in the Thieve's Guild has asked you to bring her the head of Shaun the guard from Midgaard.",
         {"emote looks at the head of Shaun.",
          "say Impressive! Well done, $1N. Welcome to the Thieve's guild."},
          800, 1 * COPPER_MULT, {""});

dilcopy qstItemsWanted@quests(QUEST_THIEVES_ENTRY, {"head@death"}, {"decapCheck@quests 0 gate_w2@midwalls"},
         {"say Excellent, thank you $1N."},
          "say I can't accept that, maybe you could bring me the head of Shaun the guard from Midgaard?");

end


/* -------------------------- GUILD GUARDS -------------------------- */


                             gate_guard

names {"guard", "figure", "hooded figure"}
title "the hooded figure"
descr "A dark hooded figure is here guarding the staircase."
extra {}
"A gloomy figure, clad in black clothes."
flags {UNIT_FL_NO_TELEPORT}
romflags {CHAR_PROTECTED}

M_HUMAN_THIEF_SWORD(160, SEX_MALE)
money 7 IRON_PIECE

dilcopy MurderBanish@guilds(GUILD_UDG_THIEF);
dilcopy AlertMembers@guilds(GUILD_UDG_THIEF, "");
dilcopy rescue@function("guard");
dilcopy teamwork@function("guard");
// dilcopy guild_guard@guild_paths(GUILD_UDG_THIEF, "down");
end


/* ================================================================== */
/*                              OBJECTS                               */
/* ================================================================== */
%objects

/* Thieves' Weapon - the dirk aids in lockpicking and increases dex by 1.
Also, by pressing the hidden button (PRESS BUTTON) the weapon sends weak
poison into the enemy (Power 10). The poison has 5 charges that renew
themselves. */

        thief_dirk
names {"Thieves' Dirk","dirk","dagger"}
title "a Thieves' Dirk"
descr "A thin and slender dirk lies here in the dirt."
extra {}
"A dirk used commonly by thieves and burglars, usable in both
lock-picking and in close-quarters combat. It seems that it is
manufactured especially for the Thieves' Guild in Midgaard. You notice a
small button on the hilt.<br/>
A fast look at the handle tells you there are 5 doses of poison left."
extra {"button on the hilt","button","hilt","sigil"}
"The handle seems to be hollow and the button controls some hidden
mechanism. A careful examination reveals poison stored in the handle - by
pressing the button you can release it into the blade. You also notice a
small sigil on the inside of the small storage space. You can only guess
its purpose but refreshing the poison would be a qualified shot.<br/>"
extra {"$identify"}
"The magic reveals Thieves Dirk to increase your overall Dexterity as
well as Brain and also your dagger skills.<br/>
By pressing the button, poison stored in the handle is released into the
tip of the blade.<br/>"
extra {"$improved identify"}
"The Dex bonus is +2, Bra bonus is +2, and Dagger Skill
bonus is +5%. The poison has a power of 10 and is restored slowly by the
blade itself.<br/>
The dirk is of craftmanship 4 and magical bonus 3.<br/>"
extra {"$wear_s"}
"You flick the dirk in your hand and grab the hilt comfortably."
extra {"$wear_o"}
"$1n pulls out a $2n and flicks it in $1s hand."
manipulate {MANIPULATE_TAKE,MANIPULATE_WIELD}
WEAPON_DEF(WPN_DAGGER, BONUS_EXCELLENT, BONUS_EXCELLENT)  // Xyzzy: 7,7
DEX_TRANSFER(BONUS_GOOD)  // Xyzzy: +2
BRA_TRANSFER(BONUS_GOOD)  // Xyzzy: +2
CHA_TRANSFER(BONUS_AVERAGE_MINUS)  // Xyzzy: -1
WEAPON_TRANSFER(WPN_DAGGER, BONUS_EXCELLENT_PLUS)  // Xyzzy: +5
dilcopy guild_restrict@function ({GUILD_UDG_THIEF},0,25,"");
cost 1 IRON_PIECE
spell 10

dilbegin recall charge_poison();
code
{
:start:
heartbeat:=PULSE_SEC*SECS_PER_MUD_DAY/24;
wait (SFB_TICK,(self.equip==WEAR_WIELD));
heartbeat:=PULSE_SEC*5;
act ("You notice a tiny drop of murky liquid forming at the tip of your "+
"Thieves' Dirk... your weapon is refreshing its poison."
,A_ALWAYS,self.outside,self,null,TO_CHAR);
sendto ("add charge",self);
goto start;
}
dilend


dilbegin recall aware dirkpoison();
var
	charge : integer;
	pwr : integer;
        flux : integer;
        vict :unitptr;
        n:integer;
	pc : unitptr;
code
{
pwr:=5;
	:init:
charge:=interrupt (SFB_MSG,((self==activator) and
        (argument=="add charge")), charge);
        heartbeat := PULSE_SEC*1;
	:start:
wait (SFB_CMD,(command ("press"))and("button" in argument)
and(self.equip==WEAR_WIELD));
block;
pc :=self.outside;
vict:=self.outside.fighting;
if (vict==null) goto no_person;
secure (vict,start);
secure (pc,start);
if (pwr<=0) goto fail;
pwr:=pwr-1;
self.extra.descr :=
"A dirk used commonly by thieves and burglars, usable in both
lock-picking and in close-quarters combat. It seems that it is
manufactured especially for the Thieves Guild in Midgaard. You notice a small
button on the hilt.<br/>
A fast look at the handle tells you there are "+itoa(pwr)+
" doses of poison left.";
flux := rnd (1,40);
act ("You flick the $2n in your fingers and press the button on the handle."
, A_ALWAYS,self.outside,self,vict,TO_CHAR);
act ("$1n flicks the $2n in $1s fingers, a drop of murky liquid appears at "+
"the tip!", A_HIDEINV,self.outside,self,vict,TO_REST);
if (flux <
(self.outside.abilities[ABIL_DEX]+self.outside.weapons[WPN_DAGGER]))
{
act ("Your lunge pierces $3n's skin and sends the poison into $3s body!"
, A_ALWAYS,self.outside,self,vict,TO_CHAR);
act ("$1n's lunge pierces $3n's skin, dark liquid drips from the wound!"
, A_SOMEONE,self.outside,self,vict,TO_NOTVICT);
act ("$1n's lunge pierces your skin, the wound burns with poison!"
, A_SOMEONE,self.outside,self,vict,TO_VICT);
n:=cast_spell(SPL_POISON,self.outside,self,vict,"");
unsecure(vict);
unsecure(pc);
goto start;
}
else
{
act ("Your lunge misses $3n by a wide margin, you could do with a few "+
"dagger lessons.", A_ALWAYS,self.outside,self,vict,TO_CHAR);
act ("$1n's lunge misses $3n by a wide margin, some dagger practice is "+
"definitely recommended.", A_SOMEONE,self.outside,self,vict,TO_NOTVICT);
act ("$1n's lunge misses you by a comfortable margin, you thank god for "+
"$1s lousy dagger skills.", A_SOMEONE,self.outside,self,vict,TO_VICT);
unsecure(vict);
unsecure(pc);
goto start;
}
	:fail:
{
act ("You press the button but no poison appears, the handle must be empty!"
, A_ALWAYS,self.outside,self,vict,TO_CHAR);
unsecure(vict);
unsecure(pc);
goto start;
}

:charge:
clear (charge);
if (pwr<5)
 pwr:=pwr+1;
self.extra.descr :=
"A dirk used commonly by thieves and burglars, usable in both
lock-picking and in close-quarters combat. It seems that it is
manufactured especially for the Thieves Guild in Midgaard. You notice a small
button hidden on the hilt.<br/>
A fast look at the handle tells you there are "+itoa(pwr)+
" doses of poison left.";
 goto init;
 goto start;

:no_person:
act ("As you press the button, a tiny drop of murky liquid appears on the "+
"tip of the blade and drips to the ground."
,A_ALWAYS,self.outside,null,null,TO_CHAR);
act ("A tiny drop of murky liquid appears on $1n's and drips to the "+
"ground.", A_SOMEONE,self.outside,null,null,TO_REST);
unsecure(pc);
unsecure(vict);
goto start;
}
dilend

end


/* ================================================================== */
/*                            ZONE RESET                              */
/* ================================================================== */

%reset

load oliver into study max 1
{
   equip midgaard/slth_sleeves  position WEAR_ARMS
   equip midgaard/slth_jerkin   position WEAR_BODY
   equip midgaard/slth_pants    position WEAR_LEGS
   equip midgaard/sword0        position WEAR_WIELD
}

load gryma into work_shop max 1
{
   equip midgaard/slth_sleeves  position WEAR_ARMS
   equip midgaard/slth_jerkin   position WEAR_BODY
   equip midgaard/slth_pants    position WEAR_LEGS
   equip midgaard/wdclb0        position WEAR_WIELD
}

load pirate into coal_room max 1
{
   equip midgaard/slth_sleeves  position WEAR_ARMS
   equip midgaard/slth_jerkin   position WEAR_BODY
   equip midgaard/slth_pants    position WEAR_LEGS
   equip midgaard/sword0        position WEAR_WIELD
}

load bisse into storage_room max 1
{
   equip midgaard/slth_sleeves  position WEAR_ARMS
   equip midgaard/slth_jerkin   position WEAR_BODY
   equip midgaard/slth_pants    position WEAR_LEGS
   equip midgaard/ham0          position WEAR_WIELD
}

load garden/chat_board into thieves_bar max 1
load marie into thieves_bar max 1
{
	equip midgaard/slth_jerkin WEAR_BODY
	equip midgaard/slth_pants WEAR_LEGS
	equip midgaard/dag0 WEAR_WIELD
}

load gate_guard into entrance max 2
{
   equip midgaard/iron_bracers  position WEAR_ARMS
   equip midgaard/iron_plate    position WEAR_BODY
   equip midgaard/iron_leggings position WEAR_LEGS
   equip midgaard/iron_boots    position WEAR_FEET
   equip midgaard/iron_gloves   position WEAR_HANDS
   equip midgaard/iron_helmet   position WEAR_HEAD
   equip midgaard/spear0        position WEAR_WIELD
}

load gate_guard into entrance max 2
{
   equip midgaard/iron_bracers  position WEAR_ARMS
   equip midgaard/iron_plate    position WEAR_BODY
   equip midgaard/iron_leggings position WEAR_LEGS
   equip midgaard/iron_boots    position WEAR_FEET
   equip midgaard/iron_gloves   position WEAR_HANDS
   equip midgaard/iron_helmet   position WEAR_HEAD
   equip midgaard/spear0        position WEAR_WIELD
}


%end

